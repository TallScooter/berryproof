plugins {
    id 'java-library'
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'net.neoforged.gradle.userdev' version '7.0.190'
}

group = mod_group_id

base {
    archivesName = "berryproof-${minecraft_version}-NeoForge"
}

version = mod_version

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenLocal()
    maven {
        name = 'NeoForged'
        url  = 'https://maven.neoforged.net/releases'
    }
    maven {
        name = "Modrinth"
        url = "https://api.modrinth.com/maven"
  }
}

dependencies {
    implementation "net.neoforged:neoforge:${neoforge_version}"

    implementation("io.github.llamalad7:mixinextras-neoforge:0.5.0") {
        exclude group: "org.ow2.asm", module: "asm-analysis"
    }
    implementation ("maven.modrinth:midnightlib:${project.midnightlib_version}")
}


runs {
    configureEach { run ->
        run.systemProperty("forge.logging.markers",           "REGISTRIES")
        run.systemProperty("forge.logging.console.level",     "debug")
        run.systemProperty("forge.enabledGameTestNamespaces", project.mod_id)
    }

    client {
        systemProperty("forge.enabledGameTestNamespaces", project.mod_id)
    }

    server {
        systemProperty("forge.enabledGameTestNamespaces", project.mod_id)
        getArguments().add("--nogui")
    }

    gameTestServer {
        systemProperty("forge.enabledGameTestNamespaces", project.mod_id)
    }

}

tasks.withType(ProcessResources).configureEach {
    def replaceProps = [
        minecraft_version      : minecraft_version,
        minecraft_version_range: minecraft_version_range,
        neoforge_version       : neoforge_version,
        neoforge_version_range : neoforge_version_range,
        loader_version_range   : loader_version_range,
        mod_id                 : mod_id,
        mod_name               : mod_name,
        mod_license            : mod_license,
        mod_version            : mod_version,
        mod_authors            : mod_authors,
        mod_description        : mod_description,
    ]
    inputs.properties replaceProps

    filesMatching('META-INF/neoforge.mods.toml') {
        expand replaceProps
    }
}

publishing {
    publications {
        create("mavenJava", MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url = uri("${project.projectDir}/repo")
        }
    }
}

configurations.matching { cfg ->
    cfg.name in [
        'compileClasspath',
        'runtimeClasspath',
        'testCompileClasspath',
        'testRuntimeClasspath'
    ]
}.all {
    resolutionStrategy {
        force "org.ow2.asm:asm-analysis:9.8"
        force "org.ow2.asm:asm-tree:9.8"
    }
}

// Zip up the datapack for publishing
tasks.register('zipDatapack', Zip) {
    def dpRoot = file("${project.projectDir}/src/main/datapack/berryproof")
    from(dpRoot)
    include '**/*'
    archiveBaseName.set("${project.mod_id}-datapack")
    archiveVersion.set(project.mod_version)
    destinationDirectory.set(layout.buildDirectory.dir('datapacks'))
}

